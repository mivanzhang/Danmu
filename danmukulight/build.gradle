apply plugin: 'com.android.library'
apply plugin: 'com.github.dcendents.android-maven'
apply plugin: 'com.jfrog.bintray'

android {
    compileSdkVersion 25
    buildToolsVersion "25.0.2"

    defaultConfig {
        minSdkVersion 21
        targetSdkVersion 25
        versionCode 1
        versionName "1.0"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    useLibrary 'org.apache.http.legacy'
}

dependencies {
    compile(group: 'com.meituan.android.common.locate', name: 'impl', version: '0.2.6_8.7.2') {
        exclude module: 'support-v4'
    }
    compile 'com.android.support:support-v4:25.0.1'
    compile 'com.android.support:support-v13:25.0.1'
    compile 'com.android.support:cardview-v7:25.0.1'
    compile 'com.android.support:appcompat-v7:25.0.1'
    compile 'com.facebook.fresco.helper:fresco-helper:1.1.8'
    compile 'com.google.code.gson:gson:2.7'
    compile 'com.android.support.constraint:constraint-layout:1.0.2'
    compile 'com.sankuai.meituan.retrofit2:retrofit-mt:1.3.0'
    compile 'com.sankuai.meituan.retrofit2:converter-gson:1.3.0'
}

// Maven Group ID
group = "com.anbetter"
// library的版本号，后面每次更新都需要更改这个值
version = "1.0.0"

// 生成jar包的task
task sourcesJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    classifier = 'sources'
}

// 生成jarDoc的task
task javadoc(type: Javadoc) {
    source = android.sourceSets.main.java.srcDirs
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
    options.encoding = "UTF-8"
    failOnError false // 忽略注释语法错误，如果用jdk1.8你的注释写的不规范就编译不过。
}

// 生成javaDoc的jar
task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives javadocJar
    archives sourcesJar
}

install {
    repositories.mavenInstaller {
        pom.project {
            name 'danmukulight'
            description '基于Fresco的大图浏览库'
            url 'https://github.com/hpdx/DanmukuLight'
            inceptionYear '2016'

            packaging 'aar'
            groupId 'com.anbetter'
            artifactId 'danmukulight'
            version '1.0.0'

            licenses {
                license {
                    name 'The Apache Software License, Version 2.0'
                    url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    distribution 'repo'
                }
            }

            scm {
                connection 'https://github.com/hpdx/DanmukuLight.git' // Git仓库地址。
                developerConnection 'https://github.com/hpdx/DanmukuLight.git' // Git仓库地址。
                url 'https://github.com/hpdx/DanmukuLight' // 项目主页。
            }

            developers {
                developer {
                    id 'android_ls'
                    name 'android_ls'
                    email 'android_ls@163.com'
                }
            }
        }
    }
}

Properties properties = new Properties()
// 读取properties的配置信息
properties.load(project.rootProject.file('local.properties').newDataInputStream())

bintray {
    user = properties.getProperty("bintray.user")
    key = properties.getProperty("bintray.apikey")
    configurations = ['archives']
    pkg {
        repo = 'maven'
        name = 'danmukulight'
        userOrg = user
        licenses = ['Apache-2.0']
        websiteUrl = 'https://github.com/hpdx/DanmukuLight'
        vcsUrl = 'https://github.com/hpdx/DanmukuLight.git'
        publish = true
        publicDownloadNumbers = true
    }
}






